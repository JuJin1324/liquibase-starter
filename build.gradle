plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'org.liquibase.gradle' version '2.1.0'
}

group = 'starter-liquibase'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    liquibaseRuntime.extendsFrom runtime
}

task copyResources(type: Copy) {
    from "${projectDir}/src/main/resources"
    into "${buildDir}/resources/main"
}
compileJava.dependsOn copyResources
update.dependsOn copyResources

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'mysql:mysql-connector-java:8.0.31'

    // Liquibase
    //implementation 'org.liquibase:liquibase-core:4.15.0'
    liquibaseRuntime 'org.liquibase:liquibase-core:4.15.0'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
    liquibaseRuntime 'info.picocli:picocli:4.6.3'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.15.0'
    liquibaseRuntime 'mysql:mysql-connector-java:8.0.31'
    liquibaseRuntime sourceSets.main.compileClasspath
    liquibaseRuntime sourceSets.main.runtimeClasspath
    liquibaseRuntime sourceSets.main.output

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

liquibase {
    activities {
        local {
            contexts 'local'
            driver "com.mysql.cj.jdbc.Driver"
            url "jdbc:mysql://localhost:3312/liquibase_local?serverTimezone=UTC&characterEncoding=UTF-8"
            username "liquibase_starter"
            password "liquibase_starter"
            changeLogFile "classpath:db/changelog/db.changelog-master.xml"
        }
        dev {
            contexts 'dev'
            driver "com.mysql.cj.jdbc.Driver"
            url "jdbc:mysql://localhost:3312/liquibase_dev?serverTimezone=UTC&characterEncoding=UTF-8"
            username "liquibasedev"
            password "liquibasedev"
            changeLogFile "classpath:db/changelog/db.changelog-master.xml"
        }
    }

    runList = "local"
}

tasks.named('test') {
    useJUnitPlatform()
}
