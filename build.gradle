import org.yaml.snakeyaml.Yaml

buildscript {
    dependencies {
        classpath "org.yaml:snakeyaml:1.30"
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'org.liquibase.gradle' version '2.1.0'
}

group = 'starter-liquibase'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    liquibaseRuntime.extendsFrom runtime
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core:4.15.0'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    annotationProcessor 'org.projectlombok:lombok'

    // Liquibase
    liquibaseRuntime 'org.liquibase:liquibase-core:4.15.0'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
    liquibaseRuntime 'info.picocli:picocli:4.6.3'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.15.0'
    liquibaseRuntime 'org.mariadb.jdbc:mariadb-java-client'
    liquibaseRuntime sourceSets.main.compileClasspath
    liquibaseRuntime sourceSets.main.runtimeClasspath
    liquibaseRuntime sourceSets.main.output

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

def app = new Yaml()
        .loadAll(new File("$projectDir/src/main/resources/application.yml").newInputStream())
        .first()

liquibase {
    activities {
        local {
            driver "org.mariadb.jdbc.Driver"
            url "jdbc:mariadb://localhost:3306/liquibase_test"
            username "liquibase"
            password "liquibase"
            changeLogFile "classpath:db/changelog/db.changelog-master.xml"
            contexts 'local'
        }
        dev {
            driver "org.mariadb.jdbc.Driver"
            url "jdbc:mariadb://localhost:3306/liquibase_dev"
            username "liquibasedev"
            password "liquibasedev"
            changeLogFile "classpath:db/changelog/db.changelog-master.xml"
            contexts 'dev'
        }
        prod {
            driver "org.mariadb.jdbc.Driver"
            url "jdbc:mariadb://localhost:3306/liquibase_prod"
            username "liquibaseprod"
            password "liquibaseprod"
            changeLogFile "classpath:db/changelog/db.changelog-master.xml"
            contexts 'prod'
        }
    }

    runList = app.spring.profiles.active
}

tasks.named('test') {
    useJUnitPlatform()
}
